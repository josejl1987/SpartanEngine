/*
Copyright(c) 2015-2026 Panos Karabelas

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and / or sell
copies of the Software, and to permit persons to whom the Software is furnished
to do so, subject to the following conditions :

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.IN NO EVENT SHALL THE AUTHORS OR
COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*/

// gpu bc5 texture compression using amd compressonator kernels
// bc5 encodes two independent channels (r and g) into 16 bytes per 4x4 block
// used for normal maps where only xy are needed (z is reconstructed)
// each 64-thread group compresses 4 bc blocks

#ifndef ASPM_HLSL
#define ASPM_HLSL
#endif

#include "compressonator/bcn_common_kernel.h"
#include "common_resources.hlsl"

uint  get_num_block_x()      { return asuint(buffer_pass.values[0].x); }
uint  get_num_total_blocks() { return asuint(buffer_pass.values[0].y); }
float get_quality()          { return buffer_pass.values[0].z; }
uint  get_input_mip_offset() { return asuint(buffer_pass.values[0].w); }
uint  get_output_offset()    { return asuint(buffer_pass.values[1].x); }
uint  get_mip_width()        { return asuint(buffer_pass.values[1].y); }
uint  get_mip_height()       { return asuint(buffer_pass.values[1].z); }
uint  get_groups_per_row()   { return asuint(buffer_pass.values[1].w); }

float4 unpack_rgba8(uint packed)
{
    return float4(
        float((packed      ) & 0xFFu) / 255.0,
        float((packed >>  8) & 0xFFu) / 255.0,
        float((packed >> 16) & 0xFFu) / 255.0,
        float((packed >> 24) & 0xFFu) / 255.0
    );
}

#define MAX_USED_THREAD   16
#define BLOCK_IN_GROUP    4
#define THREAD_GROUP_SIZE 64
#define BLOCK_SIZE_Y      4
#define BLOCK_SIZE_X      4

groupshared float4 shared_temp[THREAD_GROUP_SIZE];

[numthreads(THREAD_GROUP_SIZE, 1, 1)]
void main_cs(uint GI : SV_GroupIndex, uint3 groupID : SV_GroupID)
{
    uint num_block_x      = get_num_block_x();
    uint num_total_blocks = get_num_total_blocks();
    uint input_mip_offset = get_input_mip_offset();
    uint mip_width        = get_mip_width();
    uint mip_height       = get_mip_height();

    uint blockInGroup = GI / MAX_USED_THREAD;
    uint linear_group = groupID.y * get_groups_per_row() + groupID.x;
    uint blockID      = linear_group * BLOCK_IN_GROUP + blockInGroup;
    uint pixelBase    = blockInGroup * MAX_USED_THREAD;
    uint pixelInBlock = GI - pixelBase;

    if (blockID >= num_total_blocks)
        return;

    uint block_y = blockID / num_block_x;
    uint block_x = blockID - block_y * num_block_x;
    uint base_x  = block_x * BLOCK_SIZE_X;
    uint base_y  = block_y * BLOCK_SIZE_Y;

    if (pixelInBlock < 16)
    {
        uint px = min(base_x + pixelInBlock % 4, mip_width - 1);
        uint py = min(base_y + pixelInBlock / 4, mip_height - 1);
        shared_temp[GI] = unpack_rgba8(tex_compress_in[input_mip_offset + py * mip_width + px]);
    }

    GroupMemoryBarrierWithGroupSync();

    if (pixelInBlock == 0)
    {
        float blockU[16]; // red channel
        float blockV[16]; // green channel
        for (int i = 0; i < 16; i++)
        {
            blockU[i] = shared_temp[pixelBase + i].x;
            blockV[i] = shared_temp[pixelBase + i].y;
        }

        tex_compress_out[get_output_offset() + blockID] = CompressBlockBC5_UNORM(blockU, blockV, get_quality());
    }
}
